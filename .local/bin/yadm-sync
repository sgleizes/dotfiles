#!/usr/bin/env zsh
#
# Synchronize the master worktree with local changes in HOME.
# This will synchronize files which are tracked on the master branch, and
# all staged files in the main worktree.
#
# Create the worktree for the master branch with:
# `yadm worktree add <path> master`
#
# Usage: yadm-sync
#

rsync_opts=(-axu --no-implied-dirs --ignore-missing-args --info=name)

# List of files which are tracked on both branches and should not be auto-synced.
sync_ignore=(
  .config/yadm/gitconfig
  .config/yadm/unstable
)

main="$(yadm rev-parse --show-toplevel)"
if [[ ! "$main" ]]; then
  print -P ':: %F{red}ERROR%f: main worktree does not exist'
  return 1
fi

master="$(yadm worktree list | grep master | awk '{print $1}')"
if [[ ! "$master" ]]; then
  print -P ':: %F{red}ERROR%f: master worktree does not exist'
  return 1
fi

# Sync files which are tracked on the master branch.
rsync $rsync_opts[@] --files-from=<(git -C $master ls-files | grep -Ev ${(j:|:)sync_ignore}) $main $master
# Sync staged files from the main worktree.
rsync $rsync_opts[@] --files-from=<(yadm diff --name-only --cached) $main $master
